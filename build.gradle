plugins {
    id 'application'
    id 'distribution'
}

// disable start script. JAR file is runnable any way?
startScripts { enabled = false }

// disable default zip and tar distribution. They don't fit our preferred structure
distTar { enabled = false }
distZip { enabled = false }

// defined in settings.gradle
project.version = gradle.ext.version

// our custom zip dist.
// put jar, readme and release-info on top level
// put other documentation into docs/
task release(type: Zip){
    def baseDir = {archiveFileName.get() - '.zip'}
    into(baseDir){

        // include jar files from both sub projects
        // and make them executable
        from { project(':register').jar }
        fileMode = 0755
        from { project(':result').jar }
        fileMode = 0755

        // readme and release info
        from 'docs/README.html'
        from 'docs/release-info.txt'
        from 'docs/Manual.pdf'


        // add files required for running the application
        from 'release_data/race.properties'
        into("gui_files") {
            from 'release_data/drivers.txt'
            from 'release_data/start.txt'
            from 'release_data/end.txt'
        }

        // add acceptance tests
        into("acceptance_tests/Maraton/acceptanstestM3-Totaltid") {
            from 'result/testdata/Acceptanstester/Maraton/acceptanstestM3-Totaltid'
        }

        into("acceptance_tests/Maraton/acceptanstestM1-M2-M3") {
            from 'result/testdata/Acceptanstester/Maraton/acceptanstestM1-M2-M3'
        }

    }
}

build {
    // make the build task also run our custom distribution
    finalizedBy(release)
}
